
/**
 * Defines the properties of a stimulus and trial
 */

/**
 * ItemType defines the whether the first word is a word prior to
 * the training
 */

plugin zep_file;

enum ItemType {
    PRACTICE,       // Practice trial.

    CONTROL_BT,     // control group first word is a word before the training.
    CONTROL_AT,     // first word is a word after the training
    TEST_BT,        // test(experimental group) utterance prior to training
                    // first.
    TEST_AT,        // test an utterance after the training is presented first.
    TEST_RT         // Retention test
};


record TestItem {
    int             id;         // Number that identifies the test item.
    ItemType        type;       // the type of item.
    string          fn1;        // file name of the first stimulus.
    string          fn2;        // file name of the second stimulus.
    string          phoneme;    // the target phoneme.
    string          word;       // the word that is in the auditory utterances
    string          sentence;   // An example sentence where the word occurs.
};

// is loaded from csv.
TestItem[..] prac_items;
TestItem[..] test_items;

void append_test_item(TestItem item)
{
    test_items.size = test_items.size + 1;
    test_items[test_items.size - 1] = item;
}

// Just a temporary storage of test items
TestItem[..] tmp_test_items;


int testlist = -1;

int load_prac_stims()
{
    int err;
    string pracfn = stimuli_dir() + "prac_items.csv";
    err = prac_items.load(pracfn, true, 0);
    return err;
}

int select_list(int nthlist)
{
    int err;
    testlist = nthlist;
    string fn = stimuli_dir() + "stimuli.csv";

    if (file_exists(fn)) {
        err = tmp_test_items.load(fn, true, 0);
        if (err) return err;
    }
    else
        err = ERROR;

    if (tmp_test_items.size > 0) {
        if (nthlist >= NUM_PARTS) {
            println(
                "Error while loading stimuli bigger "
                "list chosen than anticipated."
                );
            err = ERROR;
        }
        int i = tmp_test_items.size / NUM_PARTS * nthlist;
        while( i < tmp_test_items.size / NUM_PARTS * (nthlist + 1)) {
            append_test_item(tmp_test_items[i]);
            i++;
        }
    }

    return err;
}
