/******************************************************************************\
FILE:           test_page.zm
AUTHOR:         Theo Veenker <theo.veenker@beexy.nl>
ADAPTED BY:     Maarten Duijndam <m.j.a.duijndam@uu.nl>

DESCRIPTION:

Provides a page object to show to the participant during test trials.
It handles presenting the stimulus and recording the participant's response.


HISTORY:
2011-10-25 TV   Created.
2017-01-05 MD   Adapted from a lexical decision task to a
                test page for word judgment.
                Now the participant hears two utterances of the same word.
                Optionally based on the settings in defs.zm it is possible
                to replay both utterances or one separately.

\******************************************************************************/

import stimuli;
import std_sound_output_device;
import defs;

Page test_page
{
    TestItem    item;           // trial control parameters

    init()
    {
        fill_pattern_color = TEST_PAGE_COLOR;
    }


    on_event:key_press()
    {
        // Handle left-shift (no) and right-shift (yes).
        handle_special_key(this, input_key, input_modifiers);
    }

    on_event:message()
    {
        // Process button-box message. See setup() below.
        // response.process_hit(message_arg, event_time);
    }

    // Two word sound chain
    SoundChain soundchain
    {
        SoundSequence
        {
            SoundFile word1
            {
            }

            SoundFile word2
            {
            }
        }

        SoundPlayback playback
        {
        }

        on_event:finish()
        {
            test_page.layout.show_response_buttons();
        }
    }

    // Sound chain to repeat first word.
    SoundChain soundchain_first
    {
        SoundFile word1
        {
        }

        SoundPlayback playback
        {
        }
    }

    // Sound chain to repeat first word.
    SoundChain soundchain_second
    {
        SoundFile word2
        {
        }

        SoundPlayback playback
        {
        }
    }

    bool sound_busy()
    {
        return  soundchain.is_busy         ||
                soundchain_first.is_busy   ||
                soundchain_second.is_busy;
    }

    VerticalLayout layout
    {
        init()
        {
            spacing = 50;
            height = 0;     // 0 means as large as possible
            is_visible = true;
        }


        LabelGadget word 
        {
            init()
            {
                fill_pattern_color = TEST_PAGE_STIMULUS_COLOR;
                size = 450, 100;
                
                font_family = TEST_PAGE_STIMULUS_FONT_FAMILY;
                font_size = TEST_PAGE_STIMULUS_FONT_SIZE;
                text_color = TEST_PAGE_STIMULUS_TEXT_COLOR;
                hor_alignment = ALIGN_CENTER;
                ver_alignment = ALIGN_CENTER;
            }
        }

        // button to repeat both stimuli.
        ButtonGadget button_repall
        {
            init()
            {
                text               = BUTTON_REPEAT_BOTH_TEXT;
                fill_pattern_color = TEST_PAGE_BUTTON_COLOR;
                text_color         = TEST_PAGE_BUTTON_TEXT_COLOR;
                font_size          = TEST_PAGE_BUTTON_FONT_SIZE;
                font_family        = TEST_PAGE_BUTTON_FONT_FAMILY;
            }

            on_event:activate()
            {
                if(!sound_busy())
                    soundchain.start(event_time + 50ms);
            }
        }

        // buttons to repeat either the first or the second stimulus.
        HorizontalLayout layout_repbutton
        {
            init()
            {
                spacing = 50;
                options = LAYOUT_EQUAL_WIDTH;
                is_visible = true;
            }

            // button to repeat the first
            ButtonGadget button_repfirst
            {
                init()
                {
                    text = BUTTON_REPEAT_FIRST_TEXT;
                    font_family = TEST_PAGE_BUTTON_FONT_FAMILY;
                    font_size = TEST_PAGE_BUTTON_FONT_SIZE;
                    fill_pattern_color = TEST_PAGE_BUTTON_COLOR;
                    text_color = TEST_PAGE_BUTTON_TEXT_COLOR;
                    size = 150, 75;
                }


                on_event:activate()
                {
                    if (!sound_busy())
                        soundchain_first.start(event_time + 50ms);
                }
            }

            // button to repeat the second
            ButtonGadget button_repsecond
            {
                init()
                {
                    text = BUTTON_REPEAT_SECOND_TEXT;
                    font_family = TEST_PAGE_BUTTON_FONT_FAMILY;
                    font_size = TEST_PAGE_BUTTON_FONT_SIZE;
                    fill_pattern_color = TEST_PAGE_BUTTON_COLOR;
                    text_color = TEST_PAGE_BUTTON_TEXT_COLOR;
                    size = 150, 75;
                }


                on_event:activate()
                {
                    if (!sound_busy())
                        soundchain_second.start(event_time + 50ms);
                }
            }
        }

        // Buttons to respond
        HorizontalLayout layout_resp
        {
            ButtonGadget button_respfirst
            {
                init()
                {
                    text = BUTTON_FIRST_CORRECT_TEXT;
                    font_family = TEST_PAGE_BUTTON_FONT_FAMILY;
                    font_size = TEST_PAGE_BUTTON_FONT_SIZE;
                    fill_pattern_color = TEST_PAGE_BUTTON_COLOR;
                    text_color = TEST_PAGE_BUTTON_TEXT_COLOR;
                    size = 150, 75;
                }

                on_event:activate()
                {
                    show_continue_button();
                }
            }
            
            ButtonGadget button_respsecond
            {
                init()
                {
                    text = BUTTON_REPEAT_SECOND_TEXT;
                    font_family = TEST_PAGE_BUTTON_FONT_FAMILY;
                    font_size = TEST_PAGE_BUTTON_FONT_SIZE;
                    fill_pattern_color = TEST_PAGE_BUTTON_COLOR;
                    text_color = TEST_PAGE_BUTTON_TEXT_COLOR;
                    size = 150, 75;
                }


                on_event:activate()
                {
                    show_continue_button();
                }
            }
        }

        ButtonGadget continue_button
        {
            init()
            {
                text = BUTTON_REPEAT_SECOND_TEXT;
                font_family = TEST_PAGE_BUTTON_FONT_FAMILY;
                font_size = TEST_PAGE_BUTTON_FONT_SIZE;
                fill_pattern_color = TEST_PAGE_BUTTON_COLOR;
                text_color = TEST_PAGE_BUTTON_TEXT_COLOR;
                size = 150, 75;
            }

            on_event:activate()
            {
                done(CONTINUE);
            }
        }

        void show_response_buttons()
        {
            if (SHOW_REPEAT_FIRST_BUTTON)
                layout_repbutton.button_repfirst.is_visible = true;
            if (SHOW_REPEAT_SECOND_BUTTON)
                layout_repbutton.button_repsecond.is_visible = true;
            if (SHOW_REPEAT_BOTH_BUTTON)
                button_repall.is_visible = true;
        }

        void show_continue_button()
        {
            continue_button.is_visible = true;
        }

        time start_stimulus(time t)
        {
            // Prepare stimulus presentation object.
            layout.word.text = item.word;

            soundchain.start(t);

            // Return stimulus onset time.
            return soundchain.expected_start_time;
        }


        void reset()
        {
            full_abort();

            layout.button_repall.is_visible = false;
            layout.layout_repbutton.button_repfirst.is_visible = false;
            layout.layout_repbutton.button_repsecond.is_visible = false;
            layout.layout_resp.button_respfirst.is_visible = false;
            layout.layout_resp.button_respsecond.is_visible = false;
        }
    }

    // Aborts any ongoing activity on this page and signals the initiator
    // of the trial that we're done.
    void done(int msgid)
    {
        // Just in case; abort presentation if still active.
        layout.reset();

        signal_target(msgid);   // tell caller we're done
        target = null;

        control.clear_status();
    }


    //==========================================================================


    // Performs preparatory work required before using this page.
    void setup(int ntrials=-1, bool fb=false, bool pr=false)
    {
        test_page_overlay.setup(this);

        // Show or hide prompt.
        test_page_overlay.show_prompt(pr ? 
            "is it a real word?" : "");

        // Enable or disable the progress bar.
        test_page_overlay.show_progress_bar(ntrials);   // disabled if < 0

        // Enable or disable feedback.
        test_page_overlay.enable_feedback(fb);          // disabled if false


        // Enable button-box buttons 0 and 1 for resp. no(0) and yes(1).
        control.target = this;
        control.button_box.disable_buttons();
    }


    // Performs cleaning up if necessary.
    void cleanup()
    {
        test_page_overlay.cleanup();

        control.button_box.disable_buttons();
        control.target = null;
    }


    // Starts a trial.
    void action(Object caller, TestItem it, time tref, int count=-1)
    {
        // Save the passed trial control parameters.
        item = it;


        // Show this page (if not yet done) and make tref the trial start time.
        tref = test_window1.show_test_page(this, tref + INTERTRIAL_INTERVAL);


        // Start fixation symbol and stimulus.
        time stimonset = layout.start_stimulus(tref);

        // Update progress counter (if enabled).
        test_page_overlay.update_progress_bar(count);

        // Update status line on control window. Cleared in done() above.
        control.set_status(
            string(item.id) + "  " + 
            string(item.type) + "  " + 
            string(item.word));


        // Remember who to signal when the trial is over.
        target = caller;
    }
}
